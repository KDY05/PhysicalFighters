plugins {
    id("com.gradleup.shadow") version "${shadowVersion}"
    id 'org.jetbrains.kotlin.jvm'
}

configurations {
    versionModules
}

dependencies {
    implementation project(':compatibility:api')

    versionModules project(':compatibility:v1_16_5')
    versionModules project(':compatibility:v1_20_5')
    versionModules project(':compatibility:v1_21_3')

    compileOnly("org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT")
}

shadowJar {
    archiveFileName = "PhysicalFighters-${version}.jar"

    configurations = [
        project.configurations.runtimeClasspath,
        project.configurations.versionModules
    ]

    destinationDirectory = layout.buildDirectory.dir("libs")

    from(rootProject.file('README.md')) { into 'META-INF' }
    from(rootProject.file('LICENSE')) { into 'META-INF' }
}

kotlin {
    jvmToolchain(8)
    compilerOptions {
        freeCompilerArgs.add('-Xjvm-default=all')
    }
}

// BuildConfig 클래스 자동 생성 (템플릿: src/main/templates/)
def generatedDir = layout.buildDirectory.dir("generated/sources/buildConfig")

tasks.register('generateBuildConfig', Copy) {
    from('BuildConfig.java.template') {
        rename { 'BuildConfig.java' }
        into 'io/github/kdy05/physicalFighters'
    }
    into generatedDir
    expand(
        buildDate: new Date().format('yyyyMMdd'),
        version: project.version
    )
    outputs.upToDateWhen { false }
}

sourceSets.main.java.srcDir(generatedDir)
compileJava.dependsOn(generateBuildConfig)
compileKotlin.dependsOn(generateBuildConfig)

processResources {
    def props = [version: project.version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

build {
    dependsOn shadowJar
}
