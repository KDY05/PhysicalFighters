plugins {
    id("com.gradleup.shadow") version "9.3.1"
    id 'org.jetbrains.kotlin.jvm'
}

configurations {
    versionModules
}

dependencies {
    implementation project(':compatibility:api')

    versionModules project(':compatibility:v1_16_5')
    versionModules project(':compatibility:v1_20_5')
    versionModules project(':compatibility:v1_21_3')

    compileOnly("org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT")
}

shadowJar {
    archiveFileName = "PhysicalFighters-${version}.jar"

    // Include all dependencies + version modules
    configurations = [
        project.configurations.runtimeClasspath,
        project.configurations.versionModules
    ]

    // Build to standard build/libs directory
    destinationDirectory = layout.buildDirectory.dir("libs")

    from(rootProject.file('README.md')) {
        into 'META-INF'
    }
    from(rootProject.file('LICENSE')) {
        into 'META-INF'
    }
}

kotlin {
    jvmToolchain(8)
    compilerOptions {
        freeCompilerArgs.add('-Xjvm-default=all')
    }
}

// BuildConfig 클래스 자동 생성
def generatedDir = layout.buildDirectory.dir("generated/sources/buildConfig")

tasks.register('generateBuildConfig') {
    def outputDir = generatedDir
    outputs.dir(outputDir)

    doLast {
        def buildDate = new Date().format('yyyyMMdd')
        def packageDir = outputDir.get().file("io/github/kdy05/physicalFighters").asFile
        packageDir.mkdirs()

        def buildConfigFile = new File(packageDir, "BuildConfig.java")
        buildConfigFile.text = """package io.github.kdy05.physicalFighters;

/**
 * 빌드 시점에 자동 생성되는 클래스.
 * 직접 수정하지 마세요.
 */
public final class BuildConfig {
    public static final int BUILD_NUMBER = ${buildDate};
    public static final String VERSION = "${project.version}";

    private BuildConfig() {}
}
"""
    }
}

sourceSets.main.java.srcDir(generatedDir)
compileJava.dependsOn(generateBuildConfig)
compileKotlin.dependsOn(generateBuildConfig)

processResources {
    def props = [version: project.version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

build {
    dependsOn shadowJar
}